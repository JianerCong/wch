// EVMC: Ethereum Client-VM Connector API.
// Copyright 2019 The EVMC Authors.
// Licensed under the Apache License, Version 2.0.

plugins {
    id 'com.diffplug.spotless' version '6.9.1'
}
apply from: "${rootDir}/wrapper.gradle"
allprojects {
    apply plugin: 'java-library'
    repositories {
        google()
        mavenCentral()
    }
    sourceCompatibility = '11'
    apply plugin: 'com.diffplug.spotless'
    spotless {
        java {
            // This path needs to be relative to each project
            target fileTree('.') {
                include '**/*.java'
                exclude '**/.gradle/**'
            }
            importOrder 'org.ethereum', 'java', ''
            trimTrailingWhitespace()
            endWithNewline()
            googleJavaFormat('1.7')
        }
    }
}

subprojects {
    tasks.withType(Test) {
        testLogging.showStandardStreams = true
        // If GRADLE_MAX_TEST_FORKS is not set, use half the available processors
        maxParallelForks = (System.getenv('GRADLE_MAX_TEST_FORKS') ?: (Runtime.runtime.availableProcessors().intdiv(2) ?: 1)).toInteger()
        useJUnitPlatform()
        reports {
            junitXml.required = true
        }
    }
}
